apply plugin: 'com.android.application'


def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties
if (keystorePropertiesFile.exists()) {
    keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def VERSION_MAJOR = 1
def VERSION_MINOR = 0
def VERSION_PATCH = 0
def VERSION_BUILD = 0


android {
    compileSdkVersion 25
    buildToolsVersion '24.0.3'

    defaultConfig {
        applicationId "cz.muni.fi.pv256.movio2.fk410022"
        manifestPlaceholders = [applicationLabel: "@string/app_name"]
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}"
        jackOptions {
            enabled true
        }
        testInstrumentationRunner "cz.muni.fi.pv256.movio2.fk410022.custom.TestAppRunner"
        buildConfigField "boolean", "LOGGING", 'false'
        resConfigs "en", "cs"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false // proguard
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-D"
            buildConfigField "boolean", "LOGGING", 'true'
        }
    }

    productFlavors {
        green {
            manifestPlaceholders = [flavorTheme: "@style/AppTheme"]
        }

        blue {
            applicationIdSuffix ".blue"
            versionNameSuffix "-B"
            manifestPlaceholders = [flavorTheme: "@style/MyTheme"]
        }
    }

    applicationVariants.all { variant ->
        def flavor = variant.productFlavors.get(0).name
        def build = variant.buildType.name

        def suffix = "." + flavor + "." + build
        variant.resValue "string", "account_type", defaultConfig.applicationId + suffix + ".yamoviedb"
        variant.resValue "string", "content_authority", defaultConfig.applicationId + suffix + ".provider"


        def nameSuffix = ""

        if (flavor == "blue") {
            nameSuffix = nameSuffix + " Blue"
        }

        if (build == "debug") {
            nameSuffix = nameSuffix + " Debug"
            variant.mergedFlavor.versionName = "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}.${VERSION_BUILD}"
        }


        variant.resValue "string", "app_name", "yaMovieDB" + nameSuffix
    }


    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    ext.androidSupportVersion = '7:25.1.0'
    ext.androidSupportDesignVersion = '25.1.0'
    ext.powerMockVersion = '1.6.6'
    ext.espressoVersion = '2.2.2'
    ext.stethoVersion = '1.4.1'

    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile group: 'com.squareup.retrofit2', name: 'converter-gson', version: '2.1.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'

    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'

    compile "com.android.support:palette-v$androidSupportVersion"
    compile "com.android.support:cardview-v$androidSupportVersion"
    compile "com.android.support:appcompat-v$androidSupportVersion"
    compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:1.0.0'
    compile "com.android.support:design:$androidSupportDesignVersion"
    compile "com.android.support.constraint:constraint-layout:1.0.0-alpha9"

    compile 'com.annimon:stream:1.1.3' // https://github.com/aNNiMON/Lightweight-Stream-API

    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.1.6'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.github.vicpinm:activeandroidrx:3.1.5'

    // logging
    compile 'com.jakewharton.timber:timber:4.5.1'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    debugCompile "com.facebook.stetho:stetho:$stethoVersion"
    debugCompile "com.facebook.stetho:stetho-okhttp3:$stethoVersion"

    testCompile 'junit:junit:4.12'
    testCompile "org.powermock:powermock-api-mockito:$powerMockVersion"
    testCompile "org.powermock:powermock-module-junit4-rule-agent:$powerMockVersion"
    testCompile "org.powermock:powermock-module-junit4-rule:$powerMockVersion"
    testCompile "org.powermock:powermock-module-junit4:$powerMockVersion"
    testCompile "org.powermock:powermock-classloading-xstream:$powerMockVersion"

    androidTestCompile('com.android.support.test:runner:0.5') {
        exclude group: 'com.android.support'
    }
    androidTestCompile('com.android.support.test:rules:0.5') {
        exclude group: 'com.android.support'
    }
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestCompile "com.android.support.test.espresso:espresso-core:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
}

// firebase
//apply plugin: 'com.google.gms.google-services'
